begin
  int pow(int x, int n) is
    if n == 0 then
      return 1
    else
      if n == 1 then 
        return x
      else
        int half = call pow(x, n / 2);
        if n % 2 == 0 then
          return half * half
        else
          return half * half * x
        fi
      fi
    fi
  end

  int fact(int n) is
    int i = 2;
    int acc = 1;
    while i <= n do
      acc = acc * i;
      i = i + 1
    done;
    return acc
  end

  int comb(int n, int k) is
    int p = call perm(n, k);
    int kFact = call fact(k);
    return p / kFact
  end

  int perm(int n, int k) is
    int nFact = call fact(n);
    int nMinusKFact = call fact(n - k);
    return nFact / nMinusKFact
  end

  int inf() is
    int uBound = call pow(2, 30);
    return uBound + (uBound - 1)
  end

  int ninf() is
    int lBound = call pow(-2, 31);
    return lBound
  end

  int max(int x, int y) is
    if x > y then
      return x
    else
      return y
    fi
  end

  int min(int x, int y) is
    if x < y then
      return x
    else
      return y
    fi
  end

  int abs(int x) is 
    if x < 0 then
      return -x
    else
      return x
    fi
  end

    int x = call ninf();
    println x
end