begin
    int getNumerator(pair(int, int) frac) is
      int num = fst frac;
      return num
    end

    int getDenominator(pair(int, int) frac) is
      int den = snd frac;
      return den
    end

    pair(int, int) toFraction(int num, int den) is
      pair(int, int) frac = newpair(num, den);
      return frac
    end

    pair(int, int) fractionMult(pair(int, int) frac1, pair(int, int) frac2) is
      int num1 = call getNumerator(frac1);
      int den1 = call getDenominator(frac1);
      int num2 = call getNumerator(frac2);
      int den2 = call getDenominator(frac2);
      int num = num1 * num2;
      int den = den1 * den2;
      pair(int, int) frac = call toFraction(num, den);
      pair(int, int) simplified = call simplify(frac);
      return simplified
    end

    pair(int, int) fractionDiv(pair(int, int) frac1, pair(int, int) frac2) is
      pair(int, int) inverted2 = call fractionInvert(frac2);
      pair(int, int) frac = call fractionMult(frac1, inverted2);
      pair(int, int) simplified = call simplify(frac);
      return simplified
    end

    pair(int, int) fractionAdd(pair(int, int) frac1, pair(int, int) frac2) is
      int num1 = call getNumerator(frac1);
      int den1 = call getDenominator(frac1);
      int num2 = call getNumerator(frac2);
      int den2 = call getDenominator(frac2);
      int num = num1 * den2 + num2 * den1;
      int den = den1 * den2;
      pair(int, int) frac = call toFraction(num, den);
      pair(int, int) simplified = call simplify(frac);
      return simplified
    end

    pair(int, int) fractionSub(pair(int, int) frac1, pair(int, int) frac2) is
      int num1 = call getNumerator(frac1);
      int den1 = call getDenominator(frac1);
      int num2 = call getNumerator(frac2);
      int den2 = call getDenominator(frac2);
      int num = num1 * den2 - num2 * den1;
      int den = den1 * den2;
      pair(int, int) frac = call toFraction(num, den);
      pair(int, int) simplified = call simplify(frac);
      return simplified
    end

    pair(int, int) simplify(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      int gcd = call gcd(num, den);
      num = num / gcd;
      den = den / gcd;
      pair(int, int) newFrac = call toFraction(num, den);
      return newFrac
    end

    int gcd(int a, int b) is
      if b == 0 then
        return a
      else
        int gcd = call gcd(b, a % b);
        return gcd
      fi
    end

    int lcm(int a, int b) is
      int gcd = call gcd(a, b);
      int lcm = a * (b / gcd);
      return lcm
    end

    int _roundedDiv(pair(int, int) frac, bool flag, int parity) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      int ceil = num / den;
      if num % den != 0 && flag then
        ceil = ceil + parity
      else
        skip
      fi;
      return ceil
    end

    int ceil(pair(int, int) frac) is
      bool isPos = call fractionIsPositive(frac);
      int ceiled = call _roundedDiv(frac, isPos, 1);
      return ceiled
    end

    int floor(pair(int, int) frac) is
      bool isNeg = call fractionIsNegative(frac);
      int floored = call _roundedDiv(frac, isNeg, -1);
      return floored
    end

    bool fractionPrint(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      print(num);
      print("/");
      print(den);
      return true
    end

    int pow(int x, int n) is
      if n == 0 then
        return 1
      else
        if n == 1 then 
          return x
        else
          int half = call pow(x, n / 2);
          if n % 2 == 0 then
            return half * half
          else
            return half * half * x
          fi
        fi
      fi
    end

    int inf() is
      int uBound = call pow(2, 30);
      return uBound + (uBound - 1)
    end

    pair(int, int) _preRoundedLog(int n, int base) is
      int count = 0;
      int acc = 1;
      while acc < n do
        acc = acc * base;
        count = count + 1
      done;
      pair(int, int) res = newpair(count, acc);
      return res
    end

    int flooredLog(int n, int base) is
      pair(int, int) res = call _preRoundedLog(n, base);
      int count = fst res;
      int acc = snd res;
      if acc > n then
        return count - 1
      else
        return count
      fi
    end

    int ceiledLog(int x, int n) is
      pair(int, int) res = call _preRoundedLog(x, n);
      int count = fst res;
      return count
    end

    pair(int, int) floatToFraction(int digits, int decimals) is 
      int inf = call inf();
      int infDigits = call ceiledLog(inf / 10, 10);
      int digitsDigits = call ceiledLog(digits, 10);
      int digitsToShift = infDigits - digitsDigits;
      int factor = call pow(10, digitsToShift);
      int num = digits * factor;
      int decDigits = call ceiledLog(decimals, 10);
      int decFactor = call pow(10, digitsToShift - decDigits);
      int dec = decimals * decFactor;
      pair(int, int) frac = call toFraction(num + dec, factor);
      frac = call simplify(frac);
      return frac
    end

    bool fractionPrintAsFloat(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      int inf = call inf();
      int digits = call flooredLog(inf / num, 10);
      int factor = call pow(10, digits);
      int approx = num * factor / den;
      int nums = approx / factor;
      int decs = approx % factor;
      print(nums);
      print(".");
      print(decs);
      return true
    end

    pair(int, int) fractionInvert(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      pair(int, int) inverted = call toFraction(den, num);
      return inverted
    end

    pair(int, int) fractionNegate(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      pair(int, int) negated = call toFraction(-num, den);
      return negated
    end

    bool fractionIsNegative(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      if (num < 0 && den > 0) || (num > 0 && den < 0) then
        return true
      else
        return false
      fi
    end

    bool fractionIsPositive(pair(int, int) frac) is
      bool isNeg = call fractionIsNegative(frac);
      bool isZero = call fractionIsZero(frac);
      return !isNeg && !isZero
    end

    bool fractionIsZero(pair(int, int) frac) is
      int num = call getNumerator(frac);
      return num == 0
    end

    pair(int, int) piIterative(int iteration) is
      int parity = 1;
      int num = 1;
      int den = 1;
      int count = 0;
      pair(int, int) acc = call toFraction(0, 1);
      while count < iteration do
        pair(int, int) frac = call toFraction(parity * num, den);
        acc = call fractionAdd(acc, frac);
        den = den + 2;
        count = count + 1; 
        parity = -parity
      done;
      pair(int, int) four = call toFraction(4, 1);
      pair(int, int) pi = call fractionMult(acc, four);
      return pi
    end

    pair(int, int) pi() is
      pair(int, int) pi = call toFraction(355, 113);
      return pi
    end

    pair(int, int) eIterative(int iteration) is
      int num = 1;
      int den = 1;
      int count = 0;
      pair(int, int) acc = call toFraction(0, 1);
      while count < iteration do
        pair(int, int) frac = call toFraction(num, den);
        acc = call fractionAdd(acc, frac);
        den = den * (count + 1);
        count = count + 1
      done;
      return acc
    end

    pair(int, int) e() is
      pair(int, int) e = call toFraction(2721, 1001);
      return e
    end

    pair(int, int) fractionPow(pair(int, int) frac, int n) is
    if n == 0 then
      pair(int, int) f = call toFraction(1, 1);
      return f
    else
      if n == 1 then 
        return frac
      else
        pair(int, int) half = call fractionPow(frac, n / 2);
        pair(int, int) f = call fractionMult(half, half);
        if n % 2 == 0 then
          return f
        else
          f = call fractionMult(f, frac);
          return f
        fi
      fi
    fi
    end

    pair(int, int) _sinApprox(int deg) is
      pair(int, int) x = call toFraction(deg, 100);
      pair(int, int) two = call toFraction(2, 1);
      pair(int, int) y = call fractionSub(two, x);
      pair(int, int) z = call fractionMult(x, y);
      return z
    end

    pair(int, int) sin(int deg) is
      int degPrincipal = deg % 360;
      if degPrincipal < 0 then
        degPrincipal = degPrincipal + 360
      else
        skip
      fi;
      if degPrincipal <= 90 then
        pair(int, int) x = call _sinApprox(degPrincipal);
        return x
      else
        if degPrincipal <= 180 then
          pair(int, int) x = call _sinApprox(180 - degPrincipal);
          return x
        else
          pair(int, int) x = call _sinApprox(degPrincipal - 180);
          x = call fractionNegate(x);
          return x
        fi
      fi
    end

    pair(int, int) cos(int deg) is
      pair(int, int) x = call sin(deg + 90);
      return x
    end

    pair(int, int) tan(int deg) is
      pair(int, int) x = call sin(deg);
      pair(int, int) y = call cos(deg);
      pair(int, int) z = call fractionDiv(x, y);
      return z
    end

    pair(int, int) root(pair(int, int) frac, int n, int iterations) is
      pair(int, int) coef1 = call toFraction(n - 1, n);
      pair(int, int) nFrac = call intToFraction(n);
      pair(int, int) coef2 = call fractionDiv(frac, nFrac);
      pair(int, int) two = call intToFraction(2);
      pair(int, int) acc = call fractionDiv(frac, two);
      int count = 0;
      while count < iterations do
        pair(int, int) f = call fractionMult(coef1, acc);
        pair(int, int) g = call fractionInvert(acc);
        g = call fractionPow(g, n - 1);
        g = call fractionMult(coef2, g);
        acc = call fractionAdd(f, g);
        count = count + 1
      done;
      return acc
    end

    pair(int, int) intToFraction(int n) is
      pair(int, int) f = call toFraction(n, 1);
      return f
    end

    pair(int, int) fractionCopy(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      pair(int, int) f = call toFraction(num, den);
      return f
    end

    bool _ = true;

    pair(int, int) x = call floatToFraction(3, 14);
    _ = call fractionPrint(x);
    println"";
    _ = call fractionPrintAsFloat(x)
end