begin
    int getNumerator(pair(int, int) frac) is
      int num = fst frac;
      return num
    end

    int getDenominator(pair(int, int) frac) is
      int den = snd frac;
      return den
    end

    pair(int, int) toFraction(int num, int den) is
      int _ = num / den;
      pair(int, int) frac = newpair(num, den);
      return frac
    end

    pair(int, int) fractionMult(pair(int, int) frac1, pair(int, int) frac2) is
      int num1 = call getNumerator(frac1);
      int den1 = call getDenominator(frac1);
      int num2 = call getNumerator(frac2);
      int den2 = call getDenominator(frac2);
      int num = num1 * num2;
      int den = den1 * den2;
      pair(int, int) frac = call toFraction(num, den);
      pair(int, int) simplified = call simplify(frac);
      return simplified
    end

    pair(int, int) fractionDiv(pair(int, int) frac1, pair(int, int) frac2) is
      int num2 = call getNumerator(frac2);
      int den2 = call getDenominator(frac2);
      pair(int, int) inverted2 = call toFraction(den2, num2);
      pair(int, int) frac = call fractionMult(frac1, inverted2);
      pair(int, int) simplified = call simplify(frac);
      return simplified
    end

    pair(int, int) fractionAdd(pair(int, int) frac1, pair(int, int) frac2) is
      int num1 = call getNumerator(frac1);
      int den1 = call getDenominator(frac1);
      int num2 = call getNumerator(frac2);
      int den2 = call getDenominator(frac2);
      int num = num1 * den2 + num2 * den1;
      int den = den1 * den2;
      pair(int, int) frac = call toFraction(num, den);
      pair(int, int) simplified = call simplify(frac);
      return simplified
    end

    pair(int, int) fractionSub(pair(int, int) frac1, pair(int, int) frac2) is
      int num1 = call getNumerator(frac1);
      int den1 = call getDenominator(frac1);
      int num2 = call getNumerator(frac2);
      int den2 = call getDenominator(frac2);
      int num = num1 * den2 - num2 * den1;
      int den = den1 * den2;
      pair(int, int) frac = call toFraction(num, den);
      pair(int, int) simplified = call simplify(frac);
      return simplified
    end

    pair(int, int) simplify(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      int gcd = call gcd(num, den);
      num = num / gcd;
      den = den / gcd;
      pair(int, int) newFrac = call toFraction(num, den);
      return newFrac
    end

    int gcd(int a, int b) is
      if b == 0 then
          return a
      else
          int gcd = call gcd(b, a % b);
          return gcd
      fi
    end

    int ceil(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      int ceil = num / den;
      bool isPos = call fractionIsPositive(frac);
      if num % den != 0 && isPos then
        ceil = ceil + 1
      else
        skip
      fi;
      return ceil
    end

    int floor(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      int floor = num / den;
      bool isNeg = call fractionIsNegative(frac);
      if num % den != 0 && isNeg then
        floor = floor - 1
      else
        skip
      fi;
      return floor
    end

    bool fractionPrint(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      print(num);
      print("/");
      print(den);
      return true
    end

    int pow(int x, int n) is
      if n == 0 then
        return 1
      else
        if n == 1 then 
          return x
        else
          int half = call pow(x, n / 2);
          if n % 2 == 0 then
            return half * half
          else
            return half * half * x
          fi
        fi
      fi
    end

    int inf() is
      int uBound = call pow(2, 30);
      return uBound + (uBound - 1)
    end

    bool fractionPrintAsFloat(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      int inf = call inf();
      int quot = inf / num;
      int factor = 1;
      while factor * 10 < quot do
        factor = factor * 10
      done;
      int approx = num * factor / den;
      int nums = approx / factor;
      int decs = approx % factor;
      print(nums);
      print(".");
      print(decs);
      return true
    end

    bool fractionIsNegative(pair(int, int) frac) is
      int num = call getNumerator(frac);
      int den = call getDenominator(frac);
      if (num < 0 && den > 0) || (num > 0 && den < 0) then
        return true
      else
        return false
      fi
    end

    bool fractionIsPositive(pair(int, int) frac) is
      bool isNeg = call fractionIsNegative(frac);
      bool isZero = call fractionIsZero(frac);
      return !isNeg && !isZero
    end

    bool fractionIsZero(pair(int, int) frac) is
      int num = call getNumerator(frac);
      return num == 0
    end

    pair(int, int) piIterative(int iteration) is
      int parity = 1;
      int num = 1;
      int den = 1;
      int count = 0;
      pair(int, int) acc = call toFraction(0, 1);
      while count < iteration do
        pair(int, int) frac = call toFraction(parity * num, den);
        acc = call fractionAdd(acc, frac);
        den = den + 2;
        count = count + 1; 
        parity = -parity
      done;
      pair(int, int) four = call toFraction(4, 1);
      pair(int, int) pi = call fractionMult(acc, four);
      return pi
    end

    pair(int, int) pi() is
      pair(int, int) pi = call toFraction(355, 113);
      return pi
    end

    pair(int, int) eIterative(int iteration) is
      int num = 1;
      int den = 1;
      int count = 0;
      pair(int, int) acc = call toFraction(0, 1);
      while count < iteration do
        pair(int, int) frac = call toFraction(num, den);
        acc = call fractionAdd(acc, frac);
        den = den * (count + 1);
        count = count + 1
      done;
      return acc
    end

    pair(int, int) e() is
      pair(int, int) e = call toFraction(2721, 1001);
      return e
    end

    pair(int, int) fracPow(pair(int, int) frac, int n) is
    if n == 0 then
      pair(int, int) f = call toFraction(1, 1);
      return f
    else
      if n == 1 then 
        return frac
      else
        pair(int, int) half = call fracPow(frac, n / 2);
        pair(int, int) f = call fractionMult(half, half);
        if n % 2 == 0 then
          return f
        else
          f = call fractionMult(f, frac);
          return f
        fi
      fi
    fi
    end

    bool _ = true;

    pair(int, int) f = call toFraction(1, 2);
    f = call fracPow(f, 3);
    _ = call fractionPrint(f);
    print("")
end